GLODEF:	//GLOBAL ADRESS
	CONTROLDEUID	EQU	001H
	CONTROLCDSTR	EQU	037H
	DOORDEUID	EQU	002H
	DOORMOTION	EQU	020H
	CLOCKDEUID	EQU	003H
	CLOCKSMODE	EQU	020H
	CLOCKMONTH	EQU	030H
	CLOCKDATE	EQU	031H
	CLOCKHOUR	EQU	032H
	CLOCKMINUTE	EQU	033H
	CLOCKALM01M	EQU	040H
	CLOCKALM01H	EQU	041H
	CLOCKALM02M	EQU	042H
	CLOCKALM02H	EQU	043H
	CLOCKALM03M	EQU	044H
	CLOCKALM03H	EQU	045H
	CLOCKALM04M	EQU	046H
	CLOCKALM04H	EQU	047H
	WINDOWDEUID	EQU	004H
	WINDOWMOTION	EQU	020H
	RINGDEUID	EQU	005H
	RINGMOTION	EQU	020H
	RINGSNONCH	EQU	038H
	//SERIAL SETUP
	;DEVICEID	EQU	0XXH
	CANRETCODE	EQU	0FEH
	CNTRETCODE	EQU	0FFH
	REPLACECODE	EQU	000H
	CLRCODE		EQU	001H
	SETBCODE	EQU	002H
	CPLCODE		EQU	003H
	SPRSERIAL	EQU	P3
	SPBSERIALTR	EQU	SPRSERIAL.2
	STATE		EQU	070H
	MOTION		EQU	071H
	POSITION	EQU	072H
	STORESSTART	EQU	078H
	STORESEND	EQU	07FH
	STORE0START	EQU	060H
	STORE1START	EQU	068H
	STORE2START	EQU	050H
	STORE3START	EQU	058H
	FLSSERIALMO	EQU	02EH
	FLBSERIALRET	EQU	FLSSERIALMO.0
	FLSSERIAL	EQU	02FH
	FLBRUNEMPTY	EQU	FLSSERIAL.0
	FLBWRITING	EQU	FLSSERIAL.1
	FLBSTORESTR	EQU	FLSSERIAL.2
	FLBSTORE0TR	EQU	FLSSERIAL.4
	FLBSTORE1TR	EQU	FLSSERIAL.5
	FLBSTORE2TR	EQU	FLSSERIAL.6
	FLBSTORE3TR	EQU	FLSSERIAL.7
	SFRPORT0	EQU	080H
	SFRPORT1	EQU	090H
	SFRPORT2	EQU	0A0H
	//DEFINE TIMER 2 (PWM)
	T2CON		EQU	0C8H
	TF2		EQU	T2CON.7
	TR2		EQU	T2CON.2
	CPRL2		EQU	T2CON.0
	RCAP2L		EQU	0CAH
	RCAP2H		EQU	0CBH
	TL2		EQU	0CCH
	TH2		EQU	0CDH
	ET2		EQU	IE.5
DEUDEF:	//DEFINE CONST
	DEVICEID	EQU	003H
	//DEFINE ADRESS
	IPSBUTTONS	EQU	P0
	IPBBUTSETSW	EQU	IPSBUTTONS.0
	IPBBUTMONTH	EQU	IPSBUTTONS.1
	IPBBUTDATE	EQU	IPSBUTTONS.2
	IPBBUTHOUR	EQU	IPSBUTTONS.3
	IPBBUTMINUT	EQU	IPSBUTTONS.4
	OPSNORMAL	EQU	P1
	OPSSELECT	EQU	P2
	FLSSETMODE	EQU	020H	;GLOBAL
	FLBCNSETMODE	EQU	FLSSETMODE.0
	FLBSETALARM1	EQU	FLSSETMODE.4
	FLBSETALARM2	EQU	FLSSETMODE.5
	FLBSETALARM3	EQU	FLSSETMODE.6
	FLBSETALARM4	EQU	FLSSETMODE.7
	FLSOTHER	EQU	02AH
	FLBSETMODE	EQU	FLSOTHER.0
	FLBALARMCHEC	EQU	FLSOTHER.6
	FLBBUTPRESS	EQU	FLSOTHER.7
	TIMEMONTH	EQU	030H	;GLOBAL
	TIMEDATE	EQU	031H	;GLOBAL
	TIMEHOUR	EQU	032H	;GLOBAL
	TIMEMINUTE	EQU	033H	;GLOBAL
	TIMESECOND	EQU	034H
	TIMETICK	EQU	035H
	DISPLAY0	EQU	038H
	DISPLAY1	EQU	039H
	DISPLAY2	EQU	03AH
	DISPLAY3	EQU	03BH
	DISPLAY4	EQU	03CH
	DISPLAY5	EQU	03DH
	DISPLAY6	EQU	03EH
	DISPLAY7	EQU	03FH
	ALARM01M	EQU	040H	;GLOBAL
	ALARM01H	EQU	041H	;GLOBAL
	ALARM02M	EQU	042H	;GLOBAL
	ALARM02H	EQU	043H	;GLOBAL
	ALARM03M	EQU	044H	;GLOBAL
	ALARM03H	EQU	045H	;GLOBAL
	ALARM04M	EQU	046H	;GLOBAL
	ALARM04H	EQU	047H	;GLOBAL
	ORG	0000H
		LJMP	START
	ORG	0023H
		LJMP	DSPSTA
	ORG	002BH
		LJMP	EAET2
	ORG	0040H
START:		LCALL	DSRSET
	//TIMER2 SETUP
		MOV	T2CON, #000H
		MOV	RCAP2H, #03CH
		MOV	RCAP2L, #0B0H
		MOV	TH2, RCAP2H
		MOV	TL2, RCAP2L
	//NORMAL SETUP
		MOV	TMOD, #00010001B
		MOV	TIMETICK, #020
		MOV	TIMESECOND, #000
		MOV	TIMEMINUTE, #000
		MOV	TIMEHOUR, #000
		MOV	TIMEDATE, #001
		MOV	TIMEMONTH, #001
		MOV	ALARM01H, #000
		MOV	ALARM01M, #000
		MOV	ALARM02H, #006
		MOV	ALARM02M, #000
		MOV	ALARM03H, #012
		MOV	ALARM03M, #000
		MOV	ALARM04H, #018
		MOV	ALARM04M, #000
		MOV	IPSBUTTONS, #0FFH
		MOV	FLSSETMODE, #000H
		MOV	FLSOTHER, #000H
		SETB	FLBALARMCHEC
		SETB	ET2
		SETB	EA
		SETB	TR0
		SETB	TR2
MALOOP:		LCALL	DSSMOV
	//BUTTON SET
		//CHANGE SET MODE
		JNB	FLBCNSETMODE, BUTSST
		CLR	FLBCNSETMODE
		CLR	FLBSETALARM1
		CLR	FLBSETALARM2
		CLR	FLBSETALARM3
		CLR	FLBSETALARM4
		JBC	FLBSETMODE, SETMOF
		SETB	FLBSETMODE
		AJMP	BUTSST
	SETMOF:	MOV	TH2, RCAP2H
		MOV	TL2, RCAP2L
		MOV	TIMETICK, #020
		MOV	TIMESECOND, #000
		SETB	FLBALARMCHEC
		;BUTTON CHANGE
	BUTSST:	JNB	IPBBUTSETSW, BUTSCT
		AJMP	BUTCNE
	BUTSCT:	JNB	FLBBUTPRESS, BUTSE0
		AJMP	BUTSEN
	BUTSE0:	SETB	FLBCNSETMODE
		AJMP	BUTPRE
		;MONTH SET
	BUTCNE:	JNB	IPBBUTMONTH, BUTSMT
		AJMP	BUTSOE
	BUTSMT:	JNB	FLBSETMODE, BUTPRE
		MOV	R0, #TIMEMONTH
		JNB	FLBBUTPRESS, BUTPRF
		AJMP	BUTPRL
		;DATE SET
	BUTSOE:	JNB	IPBBUTDATE, BUTSDT
		AJMP	BUTSDE
	BUTSDT:	JNB	FLBSETMODE, BUTPRE
		MOV	R0, #TIMEDATE
		JNB	FLBBUTPRESS, BUTPRF
		AJMP	BUTPRL
		;HOUR SET
	BUTSDE:	JNB	IPBBUTHOUR, BUTSHT
		AJMP	BUTSHE
	BUTSHT:	JNB	FLBSETMODE, BUTPRE
		MOV	R0, #TIMEHOUR
		JNB	FLBBUTPRESS, BUTPRF
		AJMP	BUTPRL
		;MINUTE SET
	BUTSHE:	JNB	IPBBUTMINUT, BUTSIT
		AJMP	BUTSIE
	BUTSIT:	JNB	FLBSETMODE, BUTMEX
		MOV	R0, #TIMEMINUTE
		JNB	FLBBUTPRESS, BUTPRF
		AJMP	BUTPRL
	BUTMEX:	MOV	TIMESECOND, #055
		AJMP	BUTPRE
		//IF NONE PRESS
	BUTSIE:	CLR	FLBBUTPRESS
		AJMP	BUTSEN
		//IF ANY PRESS
	BUTPRF:	INC	@R0
		MOV	TH2, RCAP2H
		MOV	TL2, RCAP2L
		MOV	TIMETICK, #020
	BUTPRE:	SETB	FLBBUTPRESS
		AJMP	BUTSEN
	BUTPRL:	MOV	A, TIMETICK
		JNZ	BUTSEN
		MOV	TIMETICK, #005
		INC	@R0
BUTSEN:	//CHECK
		;TICK CHECK
		MOV	R0, #TIMETICK
		MOV	R1, #020
		LCALL	MAXCHE
		MOV	A, TIMETICK
		JNZ	CHECKS
		MOV	TIMETICK, #020
		JB	FLBSETMODE, CHECKS
		INC	TIMESECOND
		;SECOND CHECK
	CHECKS:	MOV	R0, #TIMESECOND
		MOV	R1, #060
		LCALL	MAXCHE
		MOV	A, TIMESECOND
		CJNE	A, #060, CHECKI
		MOV	TIMESECOND, #000
		JB	FLBSETMODE, CHECKI
		INC	TIMEMINUTE
		SETB	FLBALARMCHEC
		;MINUTE CHECK
	CHECKI:	MOV	R0, #TIMEMINUTE
		MOV	R1, #060
		LCALL	MAXCHE
		MOV	A, TIMEMINUTE
		CJNE	A, #060, CHECKH
		MOV	TIMEMINUTE, #000
		JB	FLBSETMODE, CHECKH
		INC	TIMEHOUR
		;HOUR CHECK
	CHECKH:	MOV	R0, #TIMEHOUR
		MOV	R1, #024
		LCALL	MAXCHE
		MOV	A, TIMEHOUR
		CJNE	A, #024, CHECKD
		MOV	TIMEHOUR, #000
		JB	FLBSETMODE, CHECKD
		INC	TIMEDATE
		;DATE CHECK
	CHECKD:	MOV	A, TIMEMONTH
		CJNE	A, #002, CHECD0
		MOV	R0, #TIMEDATE
		MOV	R1, #029
		LCALL	MAXCHE
		MOV	A, TIMEDATE
		CJNE	A, #029, CHECDE
		AJMP	CHECDP
	CHECD0:	CJNE	A, #004, CHECD1
		AJMP	CHECDL
	CHECD1:	CJNE	A, #006, CHECD2
		AJMP	CHECDL
	CHECD2:	CJNE	A, #009, CHECD3
		AJMP	CHECDL
	CHECD3:	CJNE	A, #011, CHECDH
	CHECDL:	MOV	R0, #TIMEDATE
		MOV	R1, #031
		LCALL	MAXCHE
		MOV	A, TIMEDATE
		CJNE	A, #031, CHECDE
		AJMP	CHECDP
	CHECDH:	MOV	R0, #TIMEDATE
		MOV	R1, #032
		LCALL	MAXCHE
		MOV	A, TIMEDATE
		CJNE	A, #032, CHECDE
	CHECDP:	MOV	TIMEDATE, #001
		JB	FLBSETMODE, CHECDE
		INC	TIMEMONTH
	CHECDE:	JNZ	CHECKO
		MOV	TIMEDATE, #001
		;MONTH CHECK
	CHECKO:	MOV	R0, #TIMEMONTH
		MOV	R1, #013
		LCALL	MAXCHE
		MOV	A, TIMEMONTH
		CJNE	A, #013, CHECOE
		MOV	TIMEMONTH, #001
	CHECOE:	JNZ	CHEEND
		MOV	TIMEMONTH, #001
CHEEND:	//ALARM CHECK
		MOV	R0, #ALARM01H
		MOV	R1, #023
		LCALL	MAXCHE
		MOV	R0, #ALARM01M
		MOV	R1, #059
		LCALL	MAXCHE
		MOV	R0, #ALARM02H
		MOV	R1, #023
		LCALL	MAXCHE
		MOV	R0, #ALARM02M
		MOV	R1, #059
		LCALL	MAXCHE
		MOV	R0, #ALARM03H
		MOV	R1, #023
		LCALL	MAXCHE
		MOV	R0, #ALARM03M
		MOV	R1, #059
		LCALL	MAXCHE
		MOV	R0, #ALARM04H
		MOV	R1, #023
		LCALL	MAXCHE
		MOV	R0, #ALARM04M
		MOV	R1, #059
		LCALL	MAXCHE
		JB	FLBALARMCHEC, ALMC1S
		AJMP	ALMCEN
		;ALARM 1
	ALMC1S:	MOV	A, TIMEHOUR
		CJNE	A, ALARM01H, ALMC2S
		MOV	A, TIMEMINUTE
		CJNE	A, ALARM01M, ALMC2S
		MOV	R7, #003
		MOV	R1, #RINGDEUID
		MOV	R2, #000H
		MOV	R3, #RINGMOTION
		MOV	R4, #00000011B
		MOV	R5, #000H
		MOV	R6, #RINGSNONCH
		LCALL	DSSWRI
		AJMP	ALMSEN
		;ALARM 2
	ALMC2S:	MOV	A, TIMEHOUR
		CJNE	A, ALARM02H, ALMC3S
		MOV	A, TIMEMINUTE
		CJNE	A, ALARM02M, ALMC3S
		MOV	R1, #RINGDEUID
		MOV	R2, #000H
		MOV	R3, #RINGMOTION
		MOV	R4, #00000011B
		MOV	R5, #000H
		MOV	R6, #RINGSNONCH
		MOV	R7, #003
		LCALL	DSSWRI
		AJMP	ALMSEN
		;ALARM 3
	ALMC3S:	MOV	A, TIMEHOUR
		CJNE	A, ALARM03H, ALMC4S
		MOV	A, TIMEMINUTE
		CJNE	A, ALARM03M, ALMC4S
		MOV	R1, #RINGDEUID
		MOV	R2, #000H
		MOV	R3, #RINGMOTION
		MOV	R4, #00000011B
		MOV	R5, #000H
		MOV	R6, #RINGSNONCH
		MOV	R7, #003
		LCALL	DSSWRI
		AJMP	ALMSEN
		;ALARM 4
	ALMC4S:	MOV	A, TIMEHOUR
		CJNE	A, ALARM04H, ALMC4E
		MOV	A, TIMEMINUTE
		CJNE	A, ALARM04M, ALMC4E
		MOV	R1, #RINGDEUID
		MOV	R2, #000H
		MOV	R3, #RINGMOTION
		MOV	R4, #00000011B
		MOV	R5, #000H
		MOV	R6, #RINGSNONCH
		MOV	R7, #003
		LCALL	DSSWRI
		MOV	R1, #WINDOWDEUID
		MOV	R2, #000H
		MOV	R3, #WINDOWMOTION
		MOV	R4, #00000100B
		MOV	R5, #CNTRETCODE
		LCALL	DSSWRI
	ALMSEN:	CLR	FLBALARMCHEC
	ALMC4E:	AJMP	ALMCEN
ALMCEN:	//DISPLAY
		JNB	FLBSETMODE, DISPL0
		MOV	DPTR, #0810H
		AJMP	DISPLN
	DISPL0:	JNB	FLBSETALARM1, DISPL1
		MOV	R4, #001
		MOV	R2, ALARM01H
		MOV	R3, ALARM01M
		AJMP	DISPAN
	DISPL1:	JNB	FLBSETALARM2, DISPL2
		MOV	R4, #002
		MOV	R2, ALARM02H
		MOV	R3, ALARM02M
		AJMP	DISPAN
	DISPL2:	JNB	FLBSETALARM3, DISPL3
		MOV	R4, #003
		MOV	R2, ALARM03H
		MOV	R3, ALARM03M
		AJMP	DISPAN
	DISPL3:	JNB	FLBSETALARM4, DISPLX
		MOV	R4, #004
		MOV	R2, ALARM04H
		MOV	R3, ALARM04M
	DISPAN:	MOV	A, #0B7H
		CPL	A
		MOV	OPSSELECT, #11111110B
		MOV	OPSNORMAL, A
		LCALL	DL102C
		MOV	A, #038H
		CPL	A
		MOV	OPSSELECT, #11111101B
		MOV	OPSNORMAL, A
		LCALL	DL102C
		MOV	A, #004H
		CPL	A
		MOV	OPSSELECT, #11111011B
		MOV	OPSNORMAL, A
		LCALL	DL102C
		MOV	DPTR, #0800H
		MOV	A, R4
		MOVC	A, @A+DPTR
		CPL	A
		MOV	OPSSELECT, #11110111B
		MOV	OPSNORMAL, A
		LCALL	DL102C
		MOV	DPTR, #0810H
		MOV	R0, #002H
		MOV	R1, #11101111B
	DISPAL:	MOV	A, @R0
		MOV	B, #010
		DIV	AB
		MOVC	A, @A+DPTR
		CPL	A
		MOV	OPSSELECT, R1
		MOV	OPSNORMAL, A
		LCALL	DL102C
		MOV	A, R1
		RL	A
		MOV	R1, A
		MOV	A, B
		MOVC	A, @A+DPTR
		CPL	A
		MOV	OPSSELECT, R1
		MOV	OPSNORMAL, A
		LCALL	DL102C
		MOV	A, R1
		RL	A
		MOV	R1, A
		INC	R0
		CJNE	R1, #11111110B, DISPAL
		LJMP	MALOEN
	DISPLX:	MOV	DPTR, #0800H
	DISPLN:	MOV	R0, #TIMEMONTH
		MOV	R1, #11111110B
	DISPNL:	MOV	A, @R0
		MOV	B, #010
		DIV	AB
		MOVC	A, @A+DPTR
		CPL	A
		MOV	OPSSELECT, R1
		MOV	OPSNORMAL, A
		LCALL	DL102C
		MOV	A, R1
		RL	A
		MOV	R1, A
		MOV	A, B
		MOVC	A, @A+DPTR
		CPL	A
		MOV	OPSSELECT, R1
		MOV	OPSNORMAL, A
		LCALL	DL102C
		MOV	A, R1
		RL	A
		MOV	R1, A
		INC	R0
		CJNE	R1, #11111110B, DISPNL
	MALOEN:	LJMP	MALOOP
EAET2:		CLR	TF2
		DEC	TIMETICK
		RETI
DSPSTA:		PUSH	ACC
		PUSH	PSW
		PUSH	000H
	//CHECK TI/RI
		JBC	RI, DSPRST
		CLR	TI
		LJMP	DSPSST
	//RECEIVE PROCESS
	DSPRST:	MOV	R0, STATE
		MOV	A, SBUF
		//RECEIVE DEPUTY CHECK
		CJNE	R0, #000H, DSPRMO
		CJNE	A, #DEVICEID, DSPRCN
		MOV	STATE, #001H
		CLR	SM2
	DSPRCN:	LJMP	DSPEND
		//RECEIVE MOTION
	DSPRMO:	CJNE	R0, #001H, DSPRPO
		;IF CAN SEND
		CJNE	A, #CANRETCODE, DSPRM0
		;IF WRITTING
		JNB	FLBWRITING, DSPSIT
		LJMP	DSPIWR
	DSPSIT:	LJMP	DSPSID
		;IF CANT SEND
	DSPRM0:	CJNE	A, #CNTRETCODE, DSPRM1
		MOV	STATE, #000H
		SETB	SM2
		LJMP	DSPEND
		;MOTION STORE
	DSPRM1:	MOV	STATE, #002H
		MOV	MOTION, A
		LJMP	DSPEND
		//RECEIVE POSITION
	DSPRPO:	CJNE	R0, #002H, DSPRVL
		MOV	STATE, #003H
		MOV	POSITION, A
		LJMP	DSPEND
		//RECEIVE VALUE
	DSPRVL:	CJNE	R0, #003H, DSPRER
		MOV	STATE, #001H
		AJMP	DSPRVN
		//RECEIVE ERROR
	DSPRER:	MOV	STATE, #00FH
		LJMP	DSPEND
		//VALUE WRITE
	DSPRVN:	PUSH	001H
		MOV	R0, MOTION
		MOV	R1, POSITION
		;SFRPORT0 WRITE
	DSPRP0:	CJNE	R1, #SFRPORT0, DSPRP1
		CJNE	R0, #REPLACECODE, DSPR00	;REPLACE VALUE
		MOV	SFRPORT0, A
		LJMP	DSPRVE
	DSPR00:	CJNE	R0, #CLRCODE, DSPR01	;AND VALUE (CLR
		ANL	A, SFRPORT0
		MOV	SFRPORT0, A
		LJMP	DSPRVE
	DSPR01:	CJNE	R0, #SETBCODE, DSPR02	;OR VALUE (SETB
		ORL	A, SFRPORT0
		MOV	SFRPORT0, A
		LJMP	DSPRVE
	DSPR02:	CJNE	R0, #CPLCODE, DSPR0E	;XOR VALUE (CPL
		XRL	A, SFRPORT0
		MOV	SFRPORT0, A
	DSPR0E:	LJMP	DSPRVE
		;SFRPORT1 WRITE
	DSPRP1:	CJNE	R1, #SFRPORT2, DSPRP2
		CJNE	R0, #REPLACECODE, DSPR10	;REPLACE VALUE
		MOV	SFRPORT2, A
		LJMP	DSPRVE
	DSPR10:	CJNE	R0, #CLRCODE, DSPR11	;AND VALUE (CLR
		ANL	A, SFRPORT2
		MOV	SFRPORT2, A
		LJMP	DSPRVE
	DSPR11:	CJNE	R0, #SETBCODE, DSPR12	;OR VALUE (SETB
		ORL	A, SFRPORT2
		MOV	SFRPORT2, A
		LJMP	DSPRVE
	DSPR12:	CJNE	R0, #CPLCODE, DSPR1E	;XOR VALUE (CPL
		XRL	A, SFRPORT2
		MOV	SFRPORT2, A
	DSPR1E:	LJMP	DSPRVE
		;SFRPORT2 WRITE
	DSPRP2:	CJNE	R1, #SFRPORT2, DSPREX
		CJNE	R0, #REPLACECODE, DSPR20	;REPLACE VALUE
		MOV	SFRPORT2, A
		LJMP	DSPRVE
	DSPR20:	CJNE	R0, #CLRCODE, DSPR21	;AND VALUE (CLR
		ANL	A, SFRPORT2
		MOV	SFRPORT2, A
		LJMP	DSPRVE
	DSPR21:	CJNE	R0, #SETBCODE, DSPR22	;OR VALUE (SETB
		ORL	A, SFRPORT2
		MOV	SFRPORT2, A
		LJMP	DSPRVE
	DSPR22:	CJNE	R0, #CPLCODE, DSPR2E	;XOR VALUE (CPL
		XRL	A, SFRPORT2
		MOV	SFRPORT2, A
	DSPR2E:	LJMP	DSPRVE
		;OTHER WRITE
	DSPREX:	CJNE	R0, #REPLACECODE, DSPRV9
		MOV	@R1, A
		AJMP	DSPRVE
	DSPRV9:	CJNE	R0, #CLRCODE, DSPRVA
		ANL	A, @R1	;CLR 1011
		MOV	@R1, A
		AJMP	DSPRVE
	DSPRVA:	CJNE	R0, #SETBCODE, DSPRVB
		ORL	A, @R1	;SETB 0100
		MOV	@R1, A
		AJMP	DSPRVE
	DSPRVB:	CJNE	R0, #CPLCODE, DSPRVE
		XRL	A, @R1	;CPL 0100
		MOV	@R1, A
		AJMP	DSPRVE
	DSPRVE:	MOV	MOTION, #0FFH
		MOV	POSITION, #0FFH
		POP	001H
		LJMP	DSPEND
	//SEND PROCESS START
		//RUN EMPTY
	DSPSST:	JNB	FLBRUNEMPTY, DSPSMO
		CLR	FLBRUNEMPTY
		AJMP	DSPEND
		//SEND ID
	DSPSID:	MOV	STATE, #011H
		MOV	POSITION, #STORESSTART+1
		LCALL	DL400C
		MOV	SBUF, STORESSTART
		LCALL	DL400C
		LJMP	DSPEND
		//IF WRITING
	DSPIWR:	MOV	STATE, #01AH
		LCALL	DL400C
		MOV	SBUF, #000
		LCALL	DL400C
		LJMP	DSPEND
		//SEND MOTION
	DSPSMO:	MOV	R0, STATE
		CJNE	R0, #011H, DSPSPO
		MOV	STATE, #012H
		;IF REACH END
		MOV	R0, POSITION
		CJNE	R0, #STORESEND, DSPSM0
		AJMP	DSPSFI
		;IF SEND FINISH
	DSPSM0:	MOV	A, @R0
		CJNE	A, #CANRETCODE, DSPSM1
		AJMP	DSPSFI
	DSPSM1:	CJNE	A, #CNTRETCODE, DSPSNM
		AJMP	DSPSFI
		//SEND POSITION AND VALUE
	DSPSPO:	CJNE	R0, #012H, DSPSVA
		MOV	STATE, #013H
		AJMP	DSPSNM
	DSPSVA:	CJNE	R0, #013H, DSPSWR
		MOV	STATE, #011H
		AJMP	DSPSNM
		//IF WRITING SEND CNTRETCODE
	DSPSWR:	CJNE	R0, #01AH, DSPSER
		AJMP	DSPSF0
		//SEND ERROR
	DSPSER:	MOV	STATE, #01FH
		AJMP	DSPEND
		//SEND NORMAL
	DSPSNM:	MOV	R0, POSITION
		INC	POSITION
		MOV	SBUF, @R0
		LCALL	DL400C
		AJMP	DSPEND
		//SEND FINISH
	DSPSFI:	MOV	R0, #STORESSTART
		LCALL	DSSCLR
		CLR	FLBSTORESTR
	DSPSF0:	MOV	STATE, #000H
		MOV	POSITION, #0FFH
		SETB	FLBRUNEMPTY
		SETB	SM2
		MOV	SBUF, #CNTRETCODE
	DSPEND:	POP	000H
		POP	PSW
		POP	ACC
		RETI
DSRSET:		MOV	SCON, #10110000B
		SETB	ES
		MOV	STATE, #000H
		MOV	MOTION, #0FFH
		MOV	POSITION, #0FFH
		MOV	FLSSERIALMO, #000H
		MOV	FLSSERIAL, #000H
		MOV	R0, #STORESSTART
		LCALL	DSSCLR
		MOV	R0, #STORE0START
		LCALL	DSSCLR
		MOV	R0, #STORE1START
		LCALL	DSSCLR
		MOV	R0, #STORE2START
		LCALL	DSSCLR
		MOV	R0, #STORE3START
		LCALL	DSSCLR
		MOV	SPRSERIAL, #0FFH
DSSCLR:		MOV	A, #STORESEND-STORESSTART+1
	STOCLL:	MOV	@R0, #CNTRETCODE
		INC	R0
		DJNZ	ACC, STOCLL
		RET
DSSMOV:		;SERIAL STORE MOVE 0>S
		JB	FLBSTORESTR, DSSSEN
		JNB	FLBSTORE0TR, DSSSEN
		MOV	R0, #STORESSTART
		MOV	R1, #STORE0START
		MOV	R2, #STORESEND+1-STORESSTART
		SETB	FLBWRITING
		SETB	FLBSTORESTR
	DSSSLO:	MOV	A, @R1
		MOV	@R0, A
		INC	R0
		INC	R1
		DJNZ	R2, DSSSLO
		MOV	R0, #STORE0START
		LCALL	DSSCLR
		CLR	FLBSTORE0TR
		CLR	FLBWRITING
	DSSSEN:	JNB	FLBSTORESTR, DSSTRI
		CLR	SPBSERIALTR
		AJMP	DSSTRE
	DSSTRI:	SETB	SPBSERIALTR
		//SERIAL STORE MOVE 1>0
	DSSTRE:	JB	FLBSTORE0TR, DSS0EN
		JNB	FLBSTORE1TR, DSS0EN
		SETB	FLBSTORE0TR
		CLR	FLBSTORE1TR
		MOV	R0, #STORE0START
		MOV	R1, #STORE1START
		MOV	R2, #STORESEND+1-STORESSTART
	DSS0LO:	MOV	A, @R1
		MOV	@R0, A
		INC	R0
		INC	R1
		DJNZ	R2, DSS0LO
		MOV	R0, #STORE1START
		LCALL	DSSCLR
	DSS0EN:	//SERIAL STORE MOVE 2>1
		JB	FLBSTORE1TR, DSS1EN
		JNB	FLBSTORE2TR, DSS1EN
		SETB	FLBSTORE1TR
		CLR	FLBSTORE2TR
		MOV	R0, #STORE1START
		MOV	R1, #STORE2START
		MOV	R2, #STORESEND+1-STORESSTART
	DSS1LO:	MOV	A, @R1
		MOV	@R0, A
		INC	R0
		INC	R1
		DJNZ	R2, DSS1LO
		MOV	R0, #STORE2START
		LCALL	DSSCLR
	DSS1EN:	//SERIAL STORE MOVE 3>2
		JB	FLBSTORE2TR, DSS2EN
		JNB	FLBSTORE3TR, DSS2EN
		SETB	FLBSTORE2TR
		CLR	FLBSTORE3TR
		MOV	R0, #STORE2START
		MOV	R1, #STORE3START
		MOV	R2, #STORESEND+1-STORESSTART
	DSS2LO:	MOV	A, @R1
		MOV	@R0, A
		INC	R0
		INC	R1
		DJNZ	R2, DSS2LO
		MOV	R0, #STORE3START
		LCALL	DSSCLR
	DSS2EN:	RET
DSSWRI:		JB	FLBSTORE0TR, STPOS0
		MOV	R0, #STORE0START
		SETB	FLBSTORE0TR
		AJMP	DSSWSE
	STPOS0:	JB	FLBSTORE1TR, STPOS1
		MOV	R0, #STORE1START
		SETB	FLBSTORE1TR
		AJMP	DSSWSE
	STPOS1:	JB	FLBSTORE2TR, STPOS2
		MOV	R0, #STORE2START
		SETB	FLBSTORE2TR
		AJMP	DSSWSE
	STPOS2:	JB	FLBSTORE3TR, STPOSE
		MOV	R0, #STORE3START
		SETB	FLBSTORE3TR
		AJMP	DSSWSE
	STPOSE:	MOV	R0, #0F0H
		RET
	DSSWSE:	MOV	@R0, 001H
		INC	R0
		MOV	@R0, 002H
		INC	R0
		MOV	@R0, 003H
		INC	R0
		MOV	@R0, 004H
		INC	R0
		MOV	@R0, 005H
		INC	R0
		MOV	@R0, 006H
		INC	R0
		MOV	@R0, 007H
		INC	R0
		MOV	@R0, #CNTRETCODE
		RET
MAXCHE:		MOV	A, @R0
		SETB	C
		SUBB	A, R1
		JC	MAXCHN
		MOV	@R0, #000
	MAXCHN:	RET
DL400C:		PUSH	007H
		MOV	R7, #020
		DJNZ	R7, $
		POP	007H
		RET
DL102C:		PUSH	006H
		PUSH	007H
		MOV	R6, #002
	DL102A:	MOV	R7, #250
		DJNZ	R7, $
		DJNZ	R6, DL102A
		POP	007H
		POP	006H
		RET
TABLE:	
	ORG	0800H
		;DB	03FH, 006H, 05BH, 04FH, 066H, 06DH, 07DH, 007H, 07FH, 06FH, 077H, 000H, 000H, 000H, 000H, 000H
		DB	0BBH, 082H, 02FH, 08FH, 096H, 09DH, 0BDH, 083H, 0BFH, 09FH, 0B7H, 000H, 000H, 000H, 000H, 000H
	ORG	0810H
		;DB	0BFH, 086H, 0DBH, 0CFH, 0E6H, 0EDH, 0FDH, 087H, 0FFH, 0EFH, 0F7H, 0B8H, 0C0H, 080H, 080H, 080H
		DB	0FBH, 0C2H, 06FH, 0CFH, 0D6H, 0DDH, 0FDH, 0C3H, 0FFH, 0DFH, 0F7H, 078H, 044H, 040H, 040H, 040H
	END
